#pragma kernel CSMain
#include "MarchingCubesTable.compute"

struct Triangle 
{
    float3 vertexA;
    float3 vertexB;
    float3 vertexC;
};

AppendStructuredBuffer<Triangle> triangles;
RWStructuredBuffer<float4> vertices;

float3 area;
float maxValue;

int ToIndex(uint x, uint y, uint z) 
{
    return z * (area.x + 1) * (area.y + 1) + y * (area.z + 1) + x;
}
float3 GetMidPos(float4 v1, float4 v2) 
{
    float t = (maxValue - v1.w) / (v2.w - v1.w);
    return v1.xyz + t * (v2.xyz-v1.xyz);
}

[numthreads(8, 8, 8)]
void CSMain (int3 id : SV_DispatchThreadID)
{   
    if (id.x >= area.x || id.y >= area.y || id.z >= area.z) 
    {
        return;
    }
    
    float4 cubeCorners[8] = 
    {
        vertices[ToIndex(id.x, id.y, id.z)],
        vertices[ToIndex(id.x + 1, id.y, id.z)],
        vertices[ToIndex(id.x + 1, id.y, id.z + 1)],
        vertices[ToIndex(id.x, id.y, id.z + 1)],
        vertices[ToIndex(id.x, id.y + 1, id.z)],
        vertices[ToIndex(id.x + 1, id.y + 1, id.z)],
        vertices[ToIndex(id.x + 1, id.y + 1, id.z + 1)],
        vertices[ToIndex(id.x, id.y + 1, id.z + 1)]
    };
    
    int cubeIndex = 0;
    if (cubeCorners[0].w < maxValue) cubeIndex |= 1;
    if (cubeCorners[1].w < maxValue) cubeIndex |= 2;
    if (cubeCorners[2].w < maxValue) cubeIndex |= 4;
    if (cubeCorners[3].w < maxValue) cubeIndex |= 8;
    if (cubeCorners[4].w < maxValue) cubeIndex |= 16;
    if (cubeCorners[5].w < maxValue) cubeIndex |= 32;
    if (cubeCorners[6].w < maxValue) cubeIndex |= 64;
    if (cubeCorners[7].w < maxValue) cubeIndex |= 128;
    
    for (int i = 0; triangulation[cubeIndex][i] != -1; i += 3) 
    {
        int a0 = cornerIndexAFromEdge[triangulation[cubeIndex][i]];
        int b0 = cornerIndexBFromEdge[triangulation[cubeIndex][i]];

        int a1 = cornerIndexAFromEdge[triangulation[cubeIndex][i+1]];
        int b1 = cornerIndexBFromEdge[triangulation[cubeIndex][i+1]];

        int a2 = cornerIndexAFromEdge[triangulation[cubeIndex][i+2]];
        int b2 = cornerIndexBFromEdge[triangulation[cubeIndex][i+2]];

        Triangle tri;
        tri.vertexA = GetMidPos(cubeCorners[a0], cubeCorners[b0]);
        tri.vertexB = GetMidPos(cubeCorners[a1], cubeCorners[b1]);
        tri.vertexC = GetMidPos(cubeCorners[a2], cubeCorners[b2]);
        triangles.Append(tri);
    }
}